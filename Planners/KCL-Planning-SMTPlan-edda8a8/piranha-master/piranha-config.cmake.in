# Get current dir.
get_filename_component(_PIRANHA_CONFIG_SELF_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

# Find the deps. Alter the cmake module path.
set(_PIRANHA_CONFIG_OLD_MODULE_PATH "${CMAKE_MODULE_PATH}")
list(APPEND CMAKE_MODULE_PATH "${_PIRANHA_CONFIG_SELF_DIR}")
find_package(Threads REQUIRED)
find_package(mp++ @PIRANHA_MINIMUM_MPPP_VERSION@ REQUIRED)
@_PIRANHA_CONFIG_OPTIONAL_DEPS@
include(PiranhaFindBoost)

if(PIRANHA_WITH_BOOST_STACKTRACE)
    if(WIN32)
        find_package(DbgEng REQUIRED)
    else()
        find_package(libbacktrace REQUIRED)
    endif()
endif()

# Restore original module path.
set(CMAKE_MODULE_PATH "${_PIRANHA_CONFIG_OLD_MODULE_PATH}")
unset(_PIRANHA_CONFIG_OLD_MODULE_PATH)

include(${_PIRANHA_CONFIG_SELF_DIR}/piranha_export.cmake)

# Clean up.
unset(_PIRANHA_CONFIG_SELF_DIR)
